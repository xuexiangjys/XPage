apply plugin: 'com.android.application'
apply plugin: 'com.xuexiang.xrouter' //引用xrouter-plugin插件实现自动注册

android {
    compileSdkVersion build_versions.target_sdk
    buildToolsVersion build_versions.build_tools

    defaultConfig {
        applicationId "com.xuexiang.xpagedemo"
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode 1
        versionName "1.0"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [ moduleName : project.getName() ]
            }
        }

    }

    if (isNeedPackage.toBoolean()) {
        signingConfigs {
            release {
                storeFile file(app_release.storeFile)
                storePassword app_release.storePassword
                keyAlias app_release.keyAlias
                keyPassword app_release.keyPassword
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (isNeedPackage.toBoolean()) {
                signingConfig signingConfigs.release
            }
        }
    }

    if (isNeedPackage.toBoolean()) {
        applicationVariants.all { variant ->
            variant.outputs.all {
                if (variant.buildType.name.equals('release')) {
                    outputFileName = "xpage_demo.apk"
                }
            }
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation deps.support.app_compat
    implementation deps.support.design
    //butterknife的sdk
    implementation deps.butterknife.runtime
    annotationProcessor deps.butterknife.compiler

    implementation project(':xpage-lib')
    annotationProcessor project(':xpage-compiler')

    //页面路由
    implementation 'com.github.xuexiangjys.XRouter:xrouter-runtime:1.0.1'
    annotationProcessor 'com.github.xuexiangjys.XRouter:xrouter-compiler:1.0.1'

//    implementation 'com.github.xuexiangjys.XPage:xpage-lib:2.2.2'
//    annotationProcessor 'com.github.xuexiangjys.XPage:xpage-compiler:2.2.2'

    implementation 'com.github.xuexiangjys.XUtil:xutil-core:1.1.5'

    implementation 'com.github.githubwing:ByeBurger:1.2.3'

}
